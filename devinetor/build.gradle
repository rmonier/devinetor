plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'

    // ScriptRunner is a Maven project
    id 'maven-publish'

    // jpackage
    id "org.panteleyev.jpackageplugin" version "1.3.0"
}

version = '1.0.0'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()

    // jitpack to build ScriptRunner from source control
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.34.0'
    implementation group: 'net.harawata', name: 'appdirs', version: '1.2.1'
    implementation "com.github.rmonier:ScriptRunner:master-SNAPSHOT"
}

application {
    // Define the main class for the application.
    mainClass = 'devinetor.Devinetor'
    applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]
}

jar {
    manifest {
        attributes( 'Implementation-Title': project.name,
                    'Implementation-Version': project.version,
                    'Main-Class': application.mainClass
        )
    }

    setDuplicatesStrategy(DuplicatesStrategy.EXCLUDE);
    from {
        configurations.runtimeClasspath.filter{ it.exists() }.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

tasks.jpackage {

    input = "${buildDir}/libs"
    destination = "${buildDir}/dist"

    appName = rootProject.name
    appVersion = project.version
    type = org.panteleyev.jpackage.ImageType.DEFAULT
    vendor = "rmonier"

    mainJar = jar.archiveFileName.get()
    mainClass = application.mainClass.get()

    javaOptions = application.applicationDefaultJvmArgs

    /* LINUX */

    linuxPackageName = jar.archiveBaseName.get()
    linuxShortcut = true

    icon = "${buildDir}/resources/main/img/icones/icone.png"

    /* WINDOWS */

    winMenu = true
    winDirChooser = true
    winShortcut = true

    windows {
        winConsole = false
        icon = "${buildDir}/resources/main/img/icones/icone.ico"
    }

    /* MACOS */

    macPackageName = jar.archiveBaseName.get()

    mac {
        icon = "${buildDir}/resources/main/img/icones/icone.icns"
    }
}

build.dependsOn(javadoc)

jpackage.dependsOn(jar)